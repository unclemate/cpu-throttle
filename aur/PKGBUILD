# Maintainer: unclemate
# Contributor: unclemate

pkgname=cpu-throttle
pkgver=0.1.1
pkgrel=1
pkgdesc='Predictive CPU thermal control daemon for Linux using feed-forward prediction control'
url='https://github.com/unclemate/cpu-throttle'
arch=('x86_64')
license=('GPL-3.0-or-later')
depends=('gcc-libs' 'glibc')
makedepends=('cargo')
backup=('etc/cpu-throttle/config.toml')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "$pkgname.service"
        "$pkgname.tmpfiles"
        "$pkgname.install")
b2sums=('8181e4989a16901232d0085fdfb24906dd902bf86f598757e9de27368e63d41a616832b928934fdb273f3853acf23af24f58f1cef7cd885af9535565b20a7d48'
        '74e0d9af1c3b00885d2d99f2a5bb2ee87b75ef2ea13d3d30f03f5602ebf7fa9af29f9b4ed3869ff4f7c6474e8c2a0723cb350e713dce8c6d307a7bad26aab692'
        '025a62fe4c9fa9cdf0ff30b013a9745038c72018799d36c265ae6bc288bbd141a7a99da2061fc97357e5a9391a31a8af5c0f643d655cbc96c31d311527ae1021'
        '048cfdb50e894b3c5a77eb28aeaf1838f66bef912fbc574306370f2de7ca1985012c9359b9f700c34b6411a72a6afc35eda943571885da39a9db8ff4f87f2c5c')

# 本地构建模式（uncomment for local testing）:
# source=(".local" "$pkgname.service" "$pkgname.tmpfiles" "$pkgname.install")
# b2sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
install="$pkgname.install"

prepare() {
    export RUSTUP_TOOLCHAIN=stable
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cd "$pkgname-$pkgver"
    cargo build --frozen --release --all-features
}

check() {
    export RUSTUP_TOOLCHAIN=stable
    cd "$pkgname-$pkgver"
    cargo test --frozen --all-features
}

package() {
    cd "$pkgname-$pkgver"

    # 安装二进制文件
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"

    # 安装配置文件示例
    install -Dm0644 config.example.toml "$pkgdir/etc/cpu-throttle/config.toml"

    # 安装 systemd 服务文件
    install -Dm0644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

    # 安装 tmpfiles 配置
    install -Dm0644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

    # 安装文档
    install -Dm0644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -Dm0644 CHANGELOG.md "$pkgdir/usr/share/doc/$pkgname/CHANGELOG.md"

    # 安装许可证
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
